(ns kotobase.engine.prolly-provider-test
  (:require [clojure.test :refer [deftest is]]
            [kotobase.engine.contract :as engine]
            [kotobase.engine.prolly.provider :as provider]
            [kotobase.storage.memory :as storage-memory]))

(def crypto
  {:blind-fn #(str "blind:" %)
   :encrypt-fn identity
   :decrypt-fn identity
   :digest-fn #(str "digest:" (hash %))})

(deftest immutable-write-then-cas-publish
  (let [backend (storage-memory/memory-store)
        eng (provider/engine-from-backend backend crypto)
        s0 (engine/empty-state eng "provider/db")
        request {:database-id "provider/db" :request-id "p1"
                 :tx-data [[:db/add "alice" :name "Alice"]]}
        published (provider/transact-and-publish! eng backend "main" s0 request)
        restored (provider/restore-head eng backend "main")]
    (is (= :published (:publish-status published)))
    (is (= (get-in published [:receipt :physical-root])
           (:physical-root restored)))
    (is (= "Alice"
           (:v (first (engine/scan eng (engine/open-snapshot eng restored)
                                   ["alice" :name nil])))))))

(deftest stale-writer-cannot-overwrite-head
  (let [backend (storage-memory/memory-store)
        eng (provider/engine-from-backend backend crypto)
        stale (engine/empty-state eng "provider/db")
        base-request {:database-id "provider/db" :request-id "base"
                      :tx-data [[:db/add "e" :v 1]]}
        winner (provider/transact-and-publish! eng backend "main" stale
                                               base-request)
        loser (provider/transact-and-publish!
               eng backend "main" stale
               {:database-id "provider/db" :request-id "loser"
                :tx-data [[:db/add "e" :v 2]]})]
    (is (= :published (:publish-status winner)))
    (is (= :conflict (:publish-status loser)))
    (is (= (get-in winner [:receipt :physical-root])
           (:winner-root loser)))))
